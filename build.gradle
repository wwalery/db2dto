plugins {
  id "net.ossindex.audit" version "0.4.11"
  id 'com.diffplug.spotless' version "8.1.0"
  id "com.dorongold.task-tree" version "4.0.1"
  id 'application'
//  id 'checkstyle'
  id "com.github.johnrengelman.shadow" version "8.1.1"
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version "2.0.0"
  id 'com.github.ben-manes.versions' version '0.53.0'
}


mainClassName = 'dev.walgo.db2dto.Main'
ext.javaVersion = JavaVersion.VERSION_17

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}



repositories {
    mavenCentral()
    mavenLocal()
}

spotless {
  java {
    toggleOffOn('fmt:off', 'fmt:on')
    removeUnusedImports()
    importOrder()
    eclipse().configFile("${rootDir}/formatter.xml")
  }
}


tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror" << "-Xlint:all" << "-Xlint:-processing"
  dependsOn 'spotlessApply'
}

dependencies {
  implementation "org.immutables:value-annotations:${immutablesVersion}"

// https://mvnrepository.com/artifact/com.google.guava/guava
  implementation "com.google.guava:guava:${guavaVersion}"

// https://mvnrepository.com/artifact/commons-cli/commons-cli
  implementation "commons-cli:commons-cli:${commonsCliVersion}"
  implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"

  implementation "io.pebbletemplates:pebble:${pebbleVersion}"

  implementation "org.slf4j:slf4j-api:${slf4jVersion}"
// https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
  implementation "ch.qos.logback:logback-classic:${logbackVersion}"
// https://mvnrepository.com/artifact/ch.qos.logback/logback-core
  implementation "ch.qos.logback:logback-core:${logbackVersion}"

// https://mvnrepository.com/artifact/com.google.code.gson/gson
  implementation "com.google.code.gson:gson:${gsonVersion}"

//  implementation files("/home/walery/dev/dev.walgo/walib/build/libs/walib-0.12.0.jar")
  implementation group: 'dev.walgo', name: 'walib', version: '0.12.0'

  testImplementation "org.junit.jupiter:junit-jupiter:5.12.2"
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
  testImplementation "org.assertj:assertj-core:3.27.3"
  
  testImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.7.4'
  testImplementation group: 'org.hsqldb', name: 'sqltool', version: '2.7.4'
  testImplementation "org.postgresql:postgresql:42.7.8"
  testImplementation "org.testcontainers:postgresql:1.21.3"

}

task copyResources(type: Copy) {
  destinationDir = file('./build/libs')
  from('./resources')
}


distZip {
  dependsOn shadowJar
}

distTar {
  dependsOn shadowJar
}

startScripts {
  dependsOn copyResources, shadowJar
}

startShadowScripts {
  dependsOn jar
}

def pomContent = {
  name "${project.group}:${project.name}"
  description project.description
  url = 'https://github.com/wwalery/db2dto'
  groupId project.group
  artifactId project.name
  version project.version
  inceptionYear '2020'
  packaging 'jar'
  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
    }
  }
  developers {
    developer {
      id = 'walery'
      name = 'Walery Wysotsky'
      email = 'dev@wysotsky.info'
    }
  }
  scm {
    connection = 'scm:git:git://github.com:wwalery/db2dto.git'
    developerConnection = 'scm:git:ssh://github.com:wwalery/db2dto.git'
    url = 'https://github.com/wwalery/db2dto/tree/master'
  }
}


task pom {
  doLast {
    pom { project pomContent }.writeTo("pom.xml")
  }
}


task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

shadowJar {
  dependsOn copyResources
//  baseName = name
  archiveClassifier = ''
}

signing {
    sign publishing.publications
}

test {
  useJUnitPlatform()
  testLogging {
    events /*"passed", */ "skipped", "failed"
//    events "started", "passed", "skipped", "failed"
    exceptionFormat = 'full'
    showStandardStreams = true
  }
  afterSuite { desc, result ->
    if (!desc.parent) { // will match the outermost suite
      println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
    }
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        
            pom {
                packaging = "jar"
                name.set("DB2DTO")
                url.set("https://github.com/wwalery/db2dto")
                description.set("Flexible DTO Java classes generator. Generate classes from database metadata (schema)")
            
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection.set("scm:https://github.com/wwalery/db2dto.git")
                    developerConnection.set("scm:git@github.com:wwalery/db2dto.git")
                    url.set("https://github.com/wwalery/db2dto")
                }
                developers {
                    developer {
                        id = 'wwalery'
                        name = 'Valerii Vysotskyi'
                        email = 'dev@wysotsky.info'
                    }
                }
            }

        }
    }
}

nexusPublishing {
  repositories {
    // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
    sonatype {
      nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
      snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
      username = project.findProperty("ossrhUsername")
      password = project.findProperty("ossrhPassword")
    }
  }
}
